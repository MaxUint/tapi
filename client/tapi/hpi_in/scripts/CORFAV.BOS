// Core Fast Attack Vehicle Script

piece base,turret,flare;

static-var restore_delay;

#define SIG_AIM 2
#define SMOKEPIECE1 base
#include "smokeunit.h"
#include "exptype.h"
#include "hitweap.h"

Create()
	{
	hide flare;
	restore_delay=3000;
	start-script SmokeUnit();
	}

SetMaxReloadTime(time)
	{
	restore_delay = time * 2;
	}

RestoreAfterDelay()
	{
	sleep restore_delay;
	turn turret to y-axis 0 speed <90>;
	}

AimPrimary(heading,pitch)
	{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <90>;
	wait-for-turn turret around y-axis;
	start-script RestoreAfterDelay();
	return(TRUE);
	}

FirePrimary()
	{
	show flare;
	sleep 150;
	hide flare;
	}

AimFromPrimary(piecenum)
	{
	piecenum=turret;
	}

QueryPrimary(piecenum)
	{
	piecenum=flare;
	}

SweetSpot(piecenum)
	{
	piecenum=base;
	}

Killed( severity, corpsetype )
	{
	hide flare;
	if (severity <= 25)
		{
		corpsetype = 1;
		explode base type	BITMAPONLY | BITMAP1;
		explode flare type	BITMAPONLY | BITMAP2;
		explode turret type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	if (severity <= 50)
		{
		corpsetype = 2;
		explode base type	BITMAPONLY | BITMAP1;
		explode flare type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode turret type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		return( 0 );
		}

	if (severity <= 99)
		{
		corpsetype = 3;
		explode base type	BITMAPONLY | BITMAP1;
		explode flare type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode turret type	SHATTER | BITMAP3;
		return( 0 );
		}

	corpsetype = 3;
	explode base type	BITMAPONLY | BITMAP1;
	explode flare type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode turret type	SHATTER | EXPLODE_ON_HIT | BITMAP3;
	return( 0 );
	}
