// Arm Fast Attack Vehicle Script

piece flare1,flare2,base,gun;

static-var restore_delay,fire;

#define SIG_AIM 2
#define SMOKEPIECE1 base
#include "smokeunit.h"
#include "exptype.h"
#include "hitweap.h"

Create()
	{
	hide flare1;
	hide flare2;
	fire = 0;
	restore_delay=3000;
	start-script SmokeUnit();
	}

SetMaxReloadTime(time)
	{
	restore_delay = time * 2;
	}

RestoreAfterDelay()
	{
	sleep restore_delay;
	turn gun to y-axis 0 speed <90>;
	turn gun to x-axis 0 speed <50>;
	}

AimPrimary(heading,pitch)
	{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn gun to y-axis heading speed <90>;
	turn gun to x-axis (0-pitch) speed <50>;
	wait-for-turn gun around y-axis;
	wait-for-turn gun around x-axis;
	start-script RestoreAfterDelay();
	return(TRUE);
	}

FirePrimary()
	{
	if (fire == 0)
		{
		show flare1;
		sleep 150;
		hide flare1;
		}
	if (fire == 1)
		{
		show flare2;
		sleep 150;
		hide flare2;
		}
	fire = !fire;
	}

AimFromPrimary(piecenum)
	{
	piecenum=gun;
	}

QueryPrimary(piecenum)
	{
	piecenum=fire;
	}

SweetSpot(piecenum)
	{
	piecenum=base;
	}

Killed( severity, corpsetype )
	{
	hide flare1;
	hide flare2;
	if (severity <= 25)
		{
		corpsetype = 1;

		// Blow off the pieces in varied ways
		explode base type	BITMAPONLY | BITMAP1;
		explode flare1 type	BITMAPONLY | BITMAP2;
		explode flare2 type	BITMAPONLY | BITMAP3;
		explode gun type	BITMAPONLY | BITMAP4;
		return( 0 );
		}

	if (severity <= 50)
		{
		corpsetype = 2;

		// Blow off the pieces in varied ways
		explode base type	BITMAPONLY | BITMAP1;
		explode flare1 type	FALL | BITMAP2;
		explode flare2 type	FALL | BITMAP3;
		explode gun type	SHATTER | BITMAP4;
		return( 0 );
		}

	if (severity <= 99)
		{
		corpsetype = 3;

		// Blow off the pieces in varied ways
		explode base type	BITMAPONLY | BITMAP1;
		explode flare1 type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode flare2 type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		explode gun type	SHATTER | BITMAP4;
		return( 0 );
		}

	// If severity is over 50, the unit just disintigrated.
	corpsetype = 3;

	explode base type	BITMAPONLY | BITMAP1;
	explode flare1 type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode flare2 type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode gun type	SHATTER | EXPLODE_ON_HIT | BITMAP4;
	return( 0 );
	}
