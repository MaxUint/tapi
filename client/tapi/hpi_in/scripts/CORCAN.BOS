// Core Can Guy

piece	torso, head, firept;

// Variables

static-var	restore_delay;

// Signals

#define SIG_MOVEMENT 2
#define SIG_AIM		4

// Animations

#define ANIM_VARIABLE   TRUE
#include "..\anim3d\corcan\walk.bos"


// Smoking damage
#define SMOKEPIECE1 torso
#include "smokeunit.h"

StartMoving()
	{
	signal SIG_MOVEMENT;            // kill off other move scripts
	set-signal-mask SIG_MOVEMENT;   // so other scripts can kill us

	call-script walk();             // Initiate walk cycle
	}

StopMoving()
	{
	signal SIG_MOVEMENT;            // kill off other move scripts
	set-signal-mask SIG_MOVEMENT;   // so other scripts can kill us

	// Restore the standard position

	#define RESTORESPEED <150>

	turn pelvis to z-axis 0 speed RESTORESPEED;
	turn belly to x-axis 0 speed RESTORESPEED;

	move torso to y-axis 0 speed [1];
	turn torso to x-axis 0 speed RESTORESPEED;

	turn rthigh to x-axis 0 speed RESTORESPEED;
	turn rleg to x-axis 0 speed RESTORESPEED;
	turn rfoot to x-axis 0 speed RESTORESPEED;

	turn lthigh to x-axis 0 speed RESTORESPEED;
	turn lleg to x-axis 0 speed RESTORESPEED;
	turn lfoot to x-axis 0 speed RESTORESPEED;

	#undef RESTORESPEED
	}

Create()
	{
	hide firept;
	restore_delay = 3000;
	start-script StopMoving();			// Set standard position
	start-script SmokeUnit();
	}

SetMaxReloadTime(time)
	{
	restore_delay = time * 2;
	}

SweetSpot(piecenum)
	{
	piecenum=0;
	}

AimFromPrimary(piecenum)
	{
	piecenum=1;		// Aim from the head
	}

QueryPrimary(piecenum)
	{
	piecenum=2;
	}

RestoreAfterDelay()
	{
	// Wait for something to happen
	sleep restore_delay;

	// If we got this far without being killed, then nothing is
	// happening.  Restore the standard position
	turn head to y-axis 0 speed <90>;
	turn gun to x-axis 0 speed <45>;
	}

AimPrimary(heading,pitch)
	{
	signal SIG_AIM;					// kill off other aim scripts
	set-signal-mask SIG_AIM;		// so other scripts can kill us

	turn head to y-axis heading speed <90>;
	turn gun to x-axis (0 - pitch) speed <45>;

	wait-for-turn head around y-axis;
	wait-for-turn gun around x-axis;

	// Start a script that will wait, and restore the standard position if
	// nothing happens after a while.  It inherits the SIG_AIM mask, so
	// it will be killed if a re-aim occurrs
	start-script RestoreAfterDelay();

	return(TRUE);
	}

FirePrimary()
	{
	// Recoil
	move gun to z-axis [-1.5] speed [25];

	// Flash
	show firept;

	wait-for-move gun along z-axis;
	move gun to z-axis [0] speed [1.5];

	sleep 150;
	hide firept;
	}

#include "EXPtype.h"

Killed( severity, corpsetype )
	{
	hide firept;
	if (severity <= 25)
		{
		corpsetype = 1;
		explode belly type	BITMAPONLY | BITMAP1;
		explode firept type	BITMAPONLY | BITMAP2;
		explode ground type	BITMAPONLY | BITMAP3;
		explode gun type	BITMAPONLY | BITMAP4;
		explode head type	BITMAPONLY | BITMAP5;
		explode lfoot type	BITMAPONLY | BITMAP1;
		explode lleg type	BITMAPONLY | BITMAP2;
		explode lthigh type	BITMAPONLY | BITMAP3;
		explode pelvis type	BITMAPONLY | BITMAP4;
		explode rfoot type	BITMAPONLY | BITMAP5;
		explode rleg type	BITMAPONLY | BITMAP1;
		explode rthigh type	BITMAPONLY | BITMAP2;
		explode torso type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	if (severity <= 50)
		{
		corpsetype = 2;
		explode belly type	BITMAPONLY | BITMAP1;
		explode firept type	FALL | BITMAP2;
		explode ground type	FALL | BITMAP3;
		explode gun type	FALL | BITMAP4;
		explode head type	FALL | BITMAP5;
		explode lfoot type	FALL | BITMAP1;
		explode lleg type	FALL | BITMAP2;
		explode lthigh type	SHATTER | BITMAP3;
		explode pelvis type	FALL | BITMAP4;
		explode rfoot type	FALL | BITMAP5;
		explode rleg type	FALL | BITMAP1;
		explode rthigh type	SHATTER | BITMAP2;
		explode torso type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	if (severity <= 99)
		{
		corpsetype = 3;
		explode belly type	BITMAPONLY | BITMAP1;
		explode firept type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode ground type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		explode gun type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
		explode head type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
		explode lfoot type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode lleg type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode lthigh type	SHATTER | BITMAP3;
		explode pelvis type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
		explode rfoot type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
		explode rleg type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode rthigh type	SHATTER | BITMAP2;
		explode torso type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	corpsetype = 3;
	explode belly type	BITMAPONLY | BITMAP1;
	explode firept type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode ground type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode gun type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
	explode head type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode lfoot type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode lleg type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode lthigh type	SHATTER | EXPLODE_ON_HIT | BITMAP3;
	explode pelvis type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
	explode rfoot type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode rleg type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode rthigh type	SHATTER | EXPLODE_ON_HIT | BITMAP2;
	explode torso type	BITMAPONLY | BITMAP3;
	return( 0 );
	}
