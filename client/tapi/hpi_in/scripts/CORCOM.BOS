// Core Commander

piece torso, mlasflsh, bigflsh, nanospray, nanogun, luparm, ruparm;

// State variables
static-var	bMoving, bAiming, bCanAim;
static-var	aimtype;

#include "EXPtype.h"

// define signals
#define SIG_AIM		2

#define	ANIM_VARIABLE   bMoving
#include "..\anim3d\corcom\walk.bos"
#include "..\anim3d\corcom\walklegs.bos"
#undef	ANIM_VARIABLE

#define	ANIM_VARIABLE   TRUE
#include "..\anim3d\corcom\stand.bos"
#include "..\anim3d\corcom\bigfire.bos"

// Aiming definitions

#define AIM_NONE	0
#define AIM_DGUN	1


MotionControl()
	{
	var	moving, aiming;

	while (TRUE)
		{
		moving = bMoving;
		aiming = bAiming;

		if (moving)
			{
			if (aiming)
				{
				bCanAim = TRUE;
				call-script walklegs();
				}

			if (NOT aiming)
				{
				bCanAim = FALSE;
				call-script walk();
				}
			}

		if (NOT moving)
			{
			bCanAim = TRUE;
			call-script stand();
			}
		}
	}

Create()
	{
	hide bigflsh;
	hide mlasflsh;

	// Initial State
	bMoving = FALSE;
	bAiming = FALSE;
	bCanAim = TRUE;

	aimtype = AIM_NONE;

	// Motion control system
	start-script MotionControl();
	}

StartMoving()
	{
	bMoving = TRUE;
	}

StopMoving()
	{
	bMoving = FALSE;
	}

SweetSpot(piecenum)
	{
	piecenum=torso;
	}

QueryNanoPiece(piecenum)
	{
	piecenum=mlasflsh;
	}

RestorePosition()
	{
	aimtype = AIM_NONE;

	turn torso to y-axis 0 speed <90>;
	wait-for-turn torso around y-axis;

	bAiming = FALSE;
	}

AimFromPrimary(piecenum)
	{
	piecenum=torso;
	}

QueryPrimary(piecenum)
	{
	piecenum=mlasflsh;
	}

AimPrimary(heading,pitch)
	{
	// Don't override the big gun
	if (aimtype == AIM_DGUN)
	{	return( FALSE ); }

	signal SIG_AIM;					// kill off other aim scripts
	set-signal-mask SIG_AIM;		// so other scripts can kill us

	// Announce that we would like to aim, and wait until we can
	bAiming = TRUE;
	while (NOT bCanAim)
		{
		sleep 100;
		}

	// Aim
	turn torso to y-axis heading speed <300>;
	turn luparm to x-axis (0-pitch-<30>) speed <45>;
	wait-for-turn torso around y-axis;
	wait-for-turn luparm around x-axis;

	return( TRUE );
	}

FirePrimary()
	{
	// Muzzle flare
	show mlasflsh;
	sleep 100;
	hide mlasflsh;
	}

AimFromTertiary(piecenum)
	{
	piecenum=torso;
	}

QueryTertiary(piecenum)
	{
	piecenum=bigflsh;
	}

AimTertiary(heading,pitch)
	{
	signal SIG_AIM;					// kill off other aim scripts
	set-signal-mask SIG_AIM;		// so other scripts can kill us

	// Announce that we would like to aim, and wait until we can
	aimtype = AIM_DGUN;
	bAiming = TRUE;
	while (NOT bCanAim)
		{
		sleep 100;
		}

	// Aim
	turn torso to y-axis heading speed <300>;
	turn biggun to x-axis 0-pitch speed <150>;
	wait-for-turn torso around y-axis;
	wait-for-turn biggun around x-axis;

	return(TRUE);
	}

FireTertiary()
	{
	// Muzzle flare
	show bigflsh;
	sleep 100;
	hide bigflsh;

	call-script bigfire();				// Fire the weapon
	}

StartBuilding(heading,pitch)
	{
	signal SIG_AIM;				// kill off other aim scripts
	set-signal-mask SIG_AIM;	// so other scripts can kill us

	// Announce that we would like to aim, and wait until we can
	bAiming = TRUE;
	while (NOT bCanAim)
		{
		sleep 100;
		}

	// Aim
	turn torso to y-axis heading speed <300>;
	turn luparm to x-axis (0 - pitch - <30>) speed <45>;
	wait-for-turn torso around y-axis;
	wait-for-turn luparm around x-axis;

	// Announce that we are ready
	set INBUILDSTANCE to TRUE;
	}

TargetCleared(which)
	{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	call-script RestorePosition();
	}

StopBuilding()
	{
	// We are no longer in a position to build
	set INBUILDSTANCE to FALSE;

	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	call-script RestorePosition();
	}
